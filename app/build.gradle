apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'

def config = rootProject.extensions.getByName("ext")
def adb = android.getAdbExe().toString()

android {
    compileSdkVersion config.getAt("compile_SDK_version")
    buildToolsVersion config.getAt("build_tools_version")
    defaultConfig {
        applicationId "com.sebaslogen.blendletje"
        minSdkVersion config.getAt("min_SDK_version")
        targetSdkVersion config.getAt("target_SDK_version")
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.sebaslogen.blendletje.TestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        warning 'NewApi'
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets { // This enables sharing code between Unit and Instrumentation tests
        String sharedTestDir = "$rootDir/app/src/testShared/java"
        String sharedResourcesTestDir = "$rootDir/app/src/testShared/resources"
        test {
            java.srcDirs += sharedTestDir
            resources.srcDirs += sharedResourcesTestDir
        }
        androidTest {
            java.srcDirs += sharedTestDir
            resources.srcDirs += sharedResourcesTestDir
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:${config.getAt("support_version")}"
    compile "com.android.support:recyclerview-v7:${config.getAt("support_version")}"
    compile "com.android.support:design:${config.getAt("support_version")}"
    compile "com.android.support:cardview-v7:${config.getAt("support_version")}"
    compile "com.android.support:palette-v7:${config.getAt("support_version")}"
    compile "ch.halarious:halarious-core:${config.getAt("halarious")}"
    compile "com.google.code.gson:gson:${config.getAt("gson")}"
    compile "com.squareup.okhttp3:okhttp:${config.getAt("okhttp")}"
    compile "com.squareup.retrofit2:retrofit:${config.getAt("retrofit")}"
    compile "com.squareup.retrofit2:converter-gson:${config.getAt("retrofit")}"
    compile "com.squareup.retrofit2:adapter-rxjava:${config.getAt("retrofit")}"
    compile "io.reactivex:rxandroid:${config.getAt("rxandroid")}"
    compile "io.reactivex:rxjava:${config.getAt("rxjava")}"
    apt "com.google.dagger:dagger-compiler:${config.getAt("dagger")}"
    provided "javax.annotation:jsr250-api:${config.getAt("jsr250")}"
    compile "com.google.dagger:dagger:${config.getAt("dagger")}"
    compile "com.jakewharton.timber:timber:${config.getAt("timber")}"
    compile "com.squareup.picasso:picasso:${config.getAt("picasso")}"
    compile "com.github.florent37:picassopalette:${config.getAt("picasso_palette")}"

    apt "com.google.auto.value:auto-value:${config.getAt("auto_value")}"
    provided "com.google.auto.value:auto-value:${config.getAt("auto_value")}"

    testCompile "junit:junit:${config.getAt("junit")}"
    testCompile "org.mockito:mockito-core:${config.getAt("mockito")}"
    testCompile "org.hamcrest:hamcrest-library:${config.getAt("hamcrest")}"
    testCompile "com.squareup.okhttp3:mockwebserver:${config.getAt("okhttp")}"

    androidTestCompile "org.mockito:mockito-core:${config.getAt("mockito")}"
    androidTestCompile "com.github.crittercism.dexmaker:dexmaker:${config.getAt("dexmaker")}"
    androidTestCompile "com.github.crittercism.dexmaker:dexmaker-mockito:${config.getAt("dexmaker")}"
    androidTestCompile "com.github.crittercism.dexmaker:dexmaker-dx:${config.getAt("dexmaker")}"
    androidTestCompile "org.hamcrest:hamcrest-library:${config.getAt("hamcrest")}"
    androidTestCompile "org.hamcrest:hamcrest-core:${config.getAt("hamcrest")}"
    androidTestCompile "com.squareup.okhttp3:mockwebserver:${config.getAt("okhttp")}"
    androidTestCompile "com.github.fabioCollini:DaggerMock:${config.getAt("daggermock")}"
    androidTestCompile "com.linkedin.testbutler:test-butler-library:${config.getAt("test_butler")}"
    //Espresso dependencies
    androidTestCompile("com.android.support.test:runner:${config.getAt("support_testrunner")}") {
        exclude module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-core:${config.getAt("espresso")}") {
        exclude module: 'support-annotations'
        exclude module: 'appcompat-v7'
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:${config.getAt("espresso")}") {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'appcompat-v7'
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }
}

task installTestButler(type: Exec, dependsOn: 'installDebugTest') {
    def processBuilder = new ProcessBuilder([adb, 'install', '-r', 'test-butler-app-1.2.0.apk'])
    processBuilder.start()
}

tasks.matching { it instanceof Test }.all { // Show more test execution information
    testLogging.events = ["failed", "passed", "skipped"]
    testLogging.exceptionFormat "full"
}